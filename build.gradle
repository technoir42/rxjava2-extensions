plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
    id "signing"

    id "com.github.ben-manes.versions"
}

group = "com.github.technoir42"
version = "0.2.2"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "io.reactivex.rxjava2:rxjava:2.2.18"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.6.0"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "RxJava 2 Extensions"
                description = "Collection of handy operators and utilities for RxJava 2."
                url = "https://github.com/technoir42/rxjava2-extensions"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "technoir"
                        name = "Sergey Chelombitko"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/technoir42/rxjava2-extensions.git"
                    developerConnection = "scm:git:ssh://git@github.com/technoir42/rxjava2-extensions.git"
                    url = "https://github.com/technoir42/rxjava2-extensions"
                }
            }
        }
    }

    repositories {
        maven {
            name = "bintray"
            url = "https://api.bintray.com/maven/sch/maven/rxjava2-extensions/;publish=1"
            credentials {
                username = findProperty("BINTRAY_USER")
                password = findProperty("BINTRAY_KEY")
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    required { !version.endsWith("-SNAPSHOT") }
}
